;************************************************************************
;*			Epson LQ Series Printer				*
;************************************************************************

; The following file defines device dependent global constants.
include externs.a86

DGROUP	GROUP	DATA
		dseg

		public	CONTRL, INTIN, PTSIN, INTOUT, PTSOUT
CONTRL		rw	CONTRL_SIZE
INTOUT		rw	INTOUT_SIZE
PTSOUT		rw	PTSOUT_SIZE
INTIN		rw	INTIN_SIZE
PTSIN		rw	0
		db	'GEMVDI 3.0 Epson LQ Series Printer Driver 10/1/87'
		db	'Serial No. XXXX-0000-654321  All Rights Reserved'
		db	'Copyright (C) 1985-1987 Digital Research Inc.'
ptsin_end	rw	PTSIN_SIZE + 2 - (ptsin_end - PTSIN)/2

CGROUP	GROUP	CODE
		cseg

		public	finish_page
		public	adv_form
		public	alphaout
		public	clearmem
		public	concat
		public	dev_rect
		public	dini_dev
		public	enter_gr
		public	exit_gr
		public	init_dev
		public	scan_out
		public	s_orient
		public	set_page
		public	ini_page

		extrn	dinit_p:near
		extrn	init_p:near
		extrn	pout:near
		extrn	printer_out:near
		extrn	alpha_map:near

		public	ln_bytes
		public	ln_next
		public	sc_bytes
		public	sc_off

; Equates for ASCII characters used in this module:
CR		Equ	0Dh		;ASCII carriage return
ESC		Equ	1Bh		;ASCII escape
FORM_FEED	Equ	0Ch		;ASCII form feed
LF		Equ	0Ah		;ASCII line feed

; The alpha text attribute escape character is a DC2.
ALPHA_ESCAPE	equ	12h

; Device dependent scan size variables:
; These all get set in set_page() (below) as per requested page size
ln_bytes	dw	0		; bytes per scan line
ln_next		dw	0		; bytes to next scan line
sc_bytes	dw	0		; bytes per slice
sc_off		dw	0
m_sl_off	dw	0


;************************************************************************
;*	ini_page()							*
;*		Initialize page information.				*
;************************************************************************
ini_page:
		ret

;************************************************************************
;*  set_page()								*
;*	Set variables to accomodate requested page size.  Called once	*
;*	only, from v_opnwk() in monout.c				*
;*	Set: vq_scan() outputs - a_page, g_page				*
;************************************************************************
set_page:
	      	mov	ax, dev_tab	
		inc	ax			; page width in pixels
		shr	ax, 1
		shr	ax, 1
		shr	ax, 1			; page width in bytes
		inc	ax
		and	ax, 0fffeh		; make word boundary
		mov	ln_bytes, ax		; bytes per line
		mov	dln_byte, ax		; bytes per line
		mov	ln_next, ax		; bytes to next line
		mov	sc_off, ax		; offset to next scan line

		mov	cx, G_SL_HGT
		mul	cx			; graphics slice worth of
		mov	sc_bytes, ax		; 	bytes/line

		mov	ax, ln_bytes
		mov	cx, 8
		mul	cx
		mov	m_sl_off, ax		; 8 wires worth of bytes/line

; Number of slices that will fit on the page = chosen page size in y direction
; divided by height of a slice.
		mov	ax, dev_tab + 2	
		inc	ax		
		xor	dx, dx		
		mov	bx, G_SL_HGT	
		div	bx		
		mov	g_page, ax		; graphics slices per page

; Now do the same for alpha text slice height
		mov	ax, req_y
		add	ax, A_SL_HGT/2
		mov	bx, A_SL_HGT
		xor	dx, dx		
		div	bx		
		mov	a_page, ax		; alpha text slices per page
		ret

;************************************************************************
;*  status = dev_rect()							*
;*	Called ONCE ONLY, from v_updwk() in jmptbl.c			*
;*	Return FALSE.							*
;************************************************************************
dev_rect:
		xor	ax, ax
		ret

;************************************************************************
;*	CONCAT								*
;*		Convert the (x, y) pair passed in (BX, AX) into a	*
;*		physical address (DI) and bit offset into a byte (BX).	*
;************************************************************************
concat:
		push	ax
		push	dx

; Calculate the physical address and the bit offset.
		sub	ax, ys_min		; normalize to start of slice
		mov	dx, ln_bytes		; dx = bytes/line
		mul	dx			; ax = bytes for y position
		mov	di, bx
		shr	di, 1
		shr	di, 1
		shr	di, 1			; adjust x to byte offset
		add	di, ax			; di = byte offset to (x, y)
		and	bx, 07h			; bl = bit offset into byte

; Restore and leave.
		pop	dx
		pop	ax
		ret

;************************************************
;*  dini_dev()					*
;*  enter_gr()					*
;*  exit_gr()                                   *
;*  s_orient()					*
;*       No-op for the Epson LQ printers.	*
;************************************************
dini_dev:
enter_gr:
exit_gr:
s_orient:
		ret

;************************************************************************
;*  init_dev()								*
;*	Called ONCE ONLY, from init_g() in monoprin.c, which is called	*
;*	from v_opnwk() in monout.c					*
;************************************************************************
init_dev:
		push	bp
		mov	ax, a_page	; alpha text lines per page
		mov	page_size, al
		mov	si, offset init_printer
		call	printer_out
		pop	bp
		ret

;**********************************************************************
;*  CLEARMEM:                                                         *
;*      Clear graphics memory.  For a printer, this means clearing    *
;*      the display list and rasterizing buffer.                      *
;**********************************************************************
Clearmem:
		push	es

		mov	ax, display_list_seg
		mov	es, ax

		xor	di, di			; clear the rasterizing buffer
		xor	ax, ax
		mov	cx, buffer_size
		shr	cx, 1
		rep	stosw

		pop	es			; restore callers registers
		ret

;************************************************************************
;*  ADV_FORM:                                                         	*
;*      Do a form feed on the printer.  Allowable print area = length	*
;*	  of paper, so you don't always WANT a form feed to go out.	*
;*	Called from v_clrwk() in monout.c, and c_escape() in monoprin.c	*
;*	  case 20: advance form, case 22: clear display list		*
;*	First parameter: not used by Epson (no multiple copy support).	*
;*	Second parameter: 0 = don't do form feed if vmu = 0		*
;*			  1 = do form feed unconditionally		*
;************************************************************************
adv_form:
		push	bp
		mov	bp, sp
		mov	bx, 4[bp]		; do form feed flag
		cmp	bx, 1
		je	do_ff			; do form feed unconditionally
		cmp	vmu, 0
		je	dont_ff
do_ff:
		mov	dl, FORM_FEED
		call	pout
		mov	vmu, 0
dont_ff:
		pop 	bp
		ret

;************************************************************************
;*  ALPHAENV:								*
;*	Verify that the alphanumeric text environment is correct.  If	*
;*	not, output the commands to the printer to set the correct	*
;*	environment.							*
;************************************************************************
alphaenv:
		push	ax
		push	bx
		push	cx
		push	dx
		push	si
		push	bp

; Is the attribute environment correct?
		mov	dl, reqalpha
		mov	dh, curalpha
		cmp	dl, dh			; requested same as current?
		je	end_alphaenv

; The attribute environment is not correct.  Find out which attributes are
; incorrect and output the appropriate command to set that environment.
		mov	cx, 8			; cx = number of attributes
		xor	bx, bx			; bx = command index

alphaenv_loop:
		mov	bp, dx			; bp = saved attribute info
		and	dl, 1			; mask next requested attrib.
		and	dh, 1			; mask next current attribute
		cmp	dl, dh			; are they the same?
		je	next_alphaenv_loop

		push	bx			; save the command index
		add	bl, dl			; update index with begin/end
		shl	bl, 1			; bx = offset into word table
		mov	si, com_addr[bx]	; si -> command string
		cmp	byte ptr [si], 00h
		je	no_print		; attribute isn't supported 
		call	printer_out		; output command string
no_print:	pop	bx			; restore command index

next_alphaenv_loop:
		mov	dx, bp			; restore shifted attrib info
		shr	dl, 1			; next requested attribute
		shr	dh, 1			; next current attribute
		inc	bx
		inc	bx			; update command index
		loop	alphaenv_loop

; Save the new attribute set.
		mov	al, reqalpha
		mov	curalpha, al

end_alphaenv:
		pop	bp
		pop	si
		pop	dx
		pop	cx
		pop	bx
		pop	ax
		ret

;************************************************************************
;*  ALPHAOUT:								*
;*	Output the alpha text string in the INTIN array.  If any alpha	*
;*	attribute escape sequences are encountered, process them.	*
;************************************************************************
alphaout:
		push	bp

; Make sure that output occurs on a printer character line.
		call	vmu_sync

; Make sure the correct alpha text environment exists on the printer.
		call	alphaenv

; Get the length of the string and start the output loop.
		mov	cx, CONTRL + 6		; cx = number of characters
		mov	si, offset INTIN
		mov	bp, A_SL_HGT
alphaout_loop:
		lodsw				; get the next character
		cmp	al, ALPHA_ESCAPE
		jne	not_escape
		cmp	cx, 1			; any more characters?
		je	not_escape
		mov	bl, byte ptr [si]	; check the next character

		cmp	bl, '9'			; have I got a number?
		jle	number			; convert to decimal
		cmp	bl, 'A'			; have I got a letter?
		jl	bad_escape		; between '9' and 'A' - ignore
		sub	bl, 7			; sub 55 to convert letter
number:		sub	bl, 48			; sub 48 to convert number

		cmp	bl, 0
		jl	bad_escape		; before '0' - ignore
		cmp	bl, 35			
		jg	bad_escape		; after 'Z' - ignore
		
		cmp	bl, 16			; ignore 'G' thru 'V'
		jl	got_escape
		cmp	bl, 32
		jge	got_escape

bad_escape:
		dec	cx
		inc	si
		inc	si
		jmp	next_alphaout_loop

; An alpha attribute escape sequence has been found.  Process it.
got_escape:
		dec	cx
		inc	si
		inc	si
		xor	bh, bh
		cmp	bl, 32			; check for style setting
		jl	attrib			; (style = W - Z, or 32 - 35)
		sub	bl, 32			; calculate style table index
		shl	bl, 1

		push	si
		mov	si, styl_addr[bx]	; bx = offset into word table
		cmp	byte ptr [si], 00h	; check for unsupported style
		je	no_style
		call	printer_out
no_style:	pop 	si
		jmps	next_alphaout_loop

attrib:		mov	al, alpha_clear[bx]	; al = bit clearing mask
		and	reqalpha, al		; clear the attribute bit
		mov	al, alpha_set[bx]	; al = bit setting mask
		or	reqalpha, al		; set the bit appropriately
		call	alphaenv
		jmps	next_alphaout_loop

; Not a special character.  Just output it.  Note that a formfeed does not
; have to be replaced with anything special for this printer.
not_escape:
		cmp	al, 127			; International char?
		jna	not_intl		; If so
		push	si
		push	ds			; save some regs
		push	cx
		push	ax
		call	alpha_map		; get string in local ds
		mov	si, ax			; offset in si
		pop	ax
		pop	cx
		pop	ds
		cmp	byte ptr [si], 0	; any to send?
		je	no_send			; if so then
		call	printer_out		; print them
no_send:
		pop	si
		jmps	next_alphaout_loop	; and on to next
not_intl:
		mov	dl, al
		call	pout			; output the character

; Check for a line feed or formfeed.  Either of these characters will cause
; an advance, in which case the vmu must be updated.
		cmp	al, LF
		jne	vmu_check_ff
		add	vmu, bp			; bump by one alpha line
		call	vmu_reset
		jmps	next_alphaout_loop
vmu_check_ff:
		cmp	al, FORM_FEED
		jne	next_alphaout_loop
		mov	vmu, 0

next_alphaout_loop:
		dec	cx			
		je	end_alphaout		
		jmp	alphaout_loop		

end_alphaout:
		pop	bp
		ret

;************************************************************************
;*  VMU_SYNC:								*
;*	Adjust the position of the print head so that alpha text output	*
;*	will occur on a text line.  ("vmu" = vertical motion units)	*
;************************************************************************
vmu_sync:
; Is the current scan line the beginning of a character line?  If not,
; send out sufficient spacing to make it so.
		mov	ax, vmu
		xor	dx, dx
		mov	bx, A_SL_HGT		; bx = text slice height
		div	bx
		cmp	dx, 0			; check for remainder
		je	end_vmu_sync
		sub	bx, dx
		add	vmu, bx			; adjust vmu to next line
		call	vmu_reset

; The bx register now contains the number of pixels to be advanced.
; Put it into the printer command and send it to the device.
		mov	sized_lf + 3, bl
		mov	si, offset sized_lf
		call	printer_out

end_vmu_sync:
		ret

;************************************************************************
;*  finish_page()							*
;*	yres has to be an even multiple of G_SL_HGT, so actual length 	*
;*	of paper may be many pixels longer.  BITMAP printers need to	*
;*	bump the physical page up by the difference to leave printer	*
;*	at the start of a new piece of paper.  We check whether or not	*
;*	it's time to do this after every GRAPHICS bump of the vmu.	*
;************************************************************************
finish_page:
		push	dx

; I've JUST bumped the vmu by G_SL_HGT.  See if it's beyond the end of the
;  graphics print area.
		mov	dx, vmu
		cmp	dx, dev_tab + 2
		jle	fp_end			; NO - not time to finish_page
		
; Calculate the number of pixels to be advanced.
		mov	ax, req_y	; true length of paper in pixels
		sub	ax, dev_tab + 2	; yres = length of graphics print area
		jz	fp_end
		
; Put it into the printer command and send it to the device.
		mov	sized_lf + 3, al
		mov	si, offset sized_lf
		call	printer_out

fp_end:
		pop	dx
		ret
		
;************************************************************************
;*  VMU_RESET:								*
;*	Internal subroutine to reset vmu to top of new page if it's 	*
;*	beyond end of last page.  I.E. subtract (yres + 1) from it if 	*
;*	it's greater than or equal to (yres + 1).  Called each time the	*
;*	vmu is increased.						*
;************************************************************************
vmu_reset:
		push	dx
		mov	dx, vmu
		cmp	dx, dev_tab + 2		; >= yres+1, i.e. > yres
		jle	no_reset
		sub	dx, dev_tab + 2	; vmu >= (yres+1), subtract (yres+1)
		sub	dx, 1
		mov	vmu, dx
no_reset:  
		pop	dx
		ret

;**********************************************************************
;*  Scan_Out                                                          *
;*      SCAN and discard any trailing blanks in an output buffer.  If *
;*      data must be sent to the printer, convert it to printer       *
;*      dependent format and transmit it.  Then advance the page.     *
;*                                                                    *
;*      The Epson LQ series requires 3 bytes per column in the print  *
;*      head.                                                         *
;*                                                                    *
;*      MSB = Top wire, 8 dots per byte, 3 bytes per column.          *
;**********************************************************************
Scan_Out:
		push	es		; save the segment register we trash

		mov	ax, yw_max
		cmp	ys_max, ax	; does slice extend beyond the window?
		jg	calc_cnt	; yes:  calculate the number of slices
		mov	cx, slice_cnt	; no:  slice count is known
		jmps	scan_parms
calc_cnt:
		sub	ax, ys_min
		mov	cx, G_SL_HGT
		add	ax, cx		; for rounding (jg - v1.1b)
		xor	dx, dx
		div	cx
		mov	cx, ax		; cx = number of slices left
scan_parms:
		xor	bx, bx		; bx gets offset to the buffer
					;  (always on a paragraph boundary)
		mov	ax, Graph_Plane	; segment address of the buffer
		mov	es, ax
Scan_Next_Slice:
		push	cx		; save outer loop count

;Backscan the even numbered scan lines
		mov     cx, ln_bytes	; cx contains repeat count
		mov	di, bx		; es:di point to last byte
		add	di, cx
		dec	di
		call	back_scan
		jcxz	Setup_Next_Scan_Line	; if blank skip output
		mov	di, bx			; es:di point to first byte
		call	byte_out

Setup_Next_Scan_Line:
		mov	si, offset Graph_LF	; advance the paper to start
		call	Printer_out		; of next 24 scan lines

		add	bx, sc_bytes		; bump address 24 scan lines
		mov	ax, G_SL_HGT
		add	vmu, ax			;  & vmu to next graphics line
		cmp	vmu, 1967
		jle	kluge
		call	finish_page
		call	vmu_reset
		
kluge:		pop	cx			; restore loop count
		loop	scan_next_slice

end_scan_out:
		pop	es			; restore callers registers
		ret

;**********************************************************************
;*  Back_Scan                                                         *
;*       This routine checks for any trailing blanks in the data line.*
;*       Trailing whitespace is discarded before the line is sent to  *
;*       the printer.                                                 *
;*                                                                    *
;*  Input:                                                            *
;*       Cx = Number of bytes per line                                *
;*       Di = Address of the last byte of the first scan line         *
;*                                                                    *
;*  Output:                                                           *
;*       Cx = Number of non-blank bytes per line                      *
;*                                                                    *
;*  Registers:                                                        *
;*       Bx preserved, all others trashed                             *
;**********************************************************************
Back_Scan:
		Mov	Dx, sc_off		; make a register for speed

Check_Next_Byte:
		Push	Cx			; save the byte count
		Push	Di			; save the byte address
		Mov	Cx, G_SL_HGT		; check slice of 24 wires
Next_Byte:
		cmp	es:Byte Ptr [Di], 0
		jnz	Found_Non_Blank
		add	di, dx			; byte in next pixel line
		loop	Next_Byte

		pop	di			; decrement byte address
		dec	di
		pop	cx			; get back loop counter
		loop	Check_Next_Byte

		ret

Found_Non_Blank:
		pop	di			; clean up stack and return
		pop	cx			; count of non-blank bytes

		ret

;**********************************************************************
;*  Byte_Out                                                          *
;*       Scan and output bytes from the bitmap to the printer.        *
;*                                                                    *
;*       Graphics byte MSB = Top Wire, LSB = Bottom Wire              *
;*                                                                    *
;*  Input:                                                            *
;*       Si = Address of first byte of first scan line to output      *
;*       Cx = number of bytes to output                               *
;*                                                                    *
;*  Registers:                                                        *
;*       Bx preserved                                                 *
;**********************************************************************
Byte_Out:
		push	bx

; We need to calculate the number of graphics bytes we will send to the
; printer.  For the Epson, this is 8 * bytes to ship.
		mov	ax, cx
		shl	ax, 1
		shl	ax, 1
		shl	ax, 1

		mov	N1, al		;Put printer into graphics mode
		mov	N2, ah
		mov	si, (Offset Graph_mode)
		call	Printer_out

;Main output loop
Output_Next_Byte:
		push	cx		; save byte count
		mov	cx, 8		; always extract a whole bytes worth
		mov	bx, 8001h	;Bl = Initial shift count
					;Bh = Start Bitmask
Repeat:
		push	di		; save starting address
		call	Mask_Out	; Get the top eight bits from bitmap
					; Returns the graphics byte in <DL>
		call	Pout		; Output byte in <Dl> to printer
		add	di, m_sl_off	; point to next set of 8 wires
		call	Mask_Out	; middle top eight bits from bitmap
		call	Pout		; Output byte in <Dl> to printer
		add	di, m_sl_off	; point to next set of 8 wires
		call	Mask_Out	; Get bottom eight bits from bitmap
		call	Pout		; Output byte in <Dl> to printer

		pop	di		; reset starting address
		ror	bh, 1		; update bitmask to next bit pattern
		inc	bl		; update shift counter 
		loop	Repeat
		pop	cx
		inc	di		; next bitmap byte address
		loop	Output_Next_Byte

		pop	bx
		ret

;**********************************************************************
;*  Mask_Out                                                          *
;*       Mask out data bits from bitmap                               *
;*                                                                    *
;*  Input:                                                            *
;*       Di = NBYT, start address                                     *
;*       Bh = NBIT, mask                                              *
;*       Cx = PIXL, loop count                                        *
;*       Bl = Rotate Count                                            *
;*                                                                    *
;*  Output:                                                           *
;*       Dl  = 8 bits of bitmap information                           *
;**********************************************************************
Mask_Out:
		push	cx                      ; save the outer loop count
		push	di			; save the starting address

		mov	cx, 8			; count of bits to extract
		xor	dl, dl			; clear work register

Again:
		rol	dl, 1			; make room for next bit
		mov	al, es:[di]		; get next byte from BITMAP
		and	al, bh			; leave only the desired bit
		or	dl, al			; combine with temp result

		add	di, sc_off		; di points to dots in next
						;   even or odd pixel line
		loop	Again

; The resulting bit pattern still needs to be rotated into correct
;   printer wire position : MSB = Top, LSB = Bottom
		mov	cl, bl		; rotate bits until correct position
		rol	dl, cl

		pop	di		; restore starting address
		pop	cx		; restore outer loop count
					; return with result in Dl

		ret

;--------------------------------------------------------------------------
include	devind.a86
include devdata.a86

		dseg

		public	map_col, term_ch, dln_byte

		extrn	yw_max:word
		extrn	buffer_size:word
		extrn	display_list_seg:word
		extrn	req_y:word

;********************************************************
;* Copy of code segment variable for use in image.c	*
;********************************************************
dln_byte	dw	0

;********************************************************
;*  Set bottom margin to 0, set form length in lines	*
;*     (page_size gets filled in when ESC is sent)	*
;********************************************************
init_printer	db	5, ESC, 'O', ESC, 'C'
page_size	db	0

;****************************************************************
;*   Graphics line feed: advance paper 24 180ths of an inch	*
;****************************************************************
Graph_LF	Db	4, Cr, Esc, 'J', 24

;************************************************************************
;*  Enter Triple Density 180 x 180 dpi graphics mode ESC, *, n1, n2	*
;************************************************************************
Graph_Mode	Db	05, Esc, '*', 39
N1		Rs	01
N2		Rs	01

;**************************************************
;*
;**************************************************
Term_Ch		Dw	0
Map_Col		Dw	0		;Black
		Dw	1		;White

;************************
;*  Alpha text locals.	*
;************************
; alpha text attribute bit clearing/setting mask table.
alpha_clear	db	0ffh, 0feh
		db	0ffh, 0fdh
		db	0ffh, 0fbh
		db	0ffh, 0f7h
		db	0ffh, 0efh
		db	0ffh, 0dfh
		db	0ffh, 0bfh
		db	0ffh, 07fh
alpha_set	db	01h, 0
		db	02h, 0
		db	04h, 0
		db	08h, 0
		db	10h, 0
		db	20h, 0
		db	40h, 0
		db	80h, 0

; Pointer table for printer commands.
com_addr	dw	end_bold
		dw	begin_bold
		dw	end_italic
		dw	begin_italic
		dw	end_under
		dw	begin_under
		dw	end_super
		dw	begin_super
		dw	end_sub
		dw	begin_sub
		dw	end_lq
		dw	begin_lq
		dw	end_expand
		dw	begin_expand
		dw	end_light
		dw	begin_light

styl_addr	dw	set_pica
		dw	set_elite
		dw	set_cmpres
		dw	set_propor

; Printer attribute commands (length, command string).
; (NOTE: These are the same as for the EHI driver.)
end_bold	db	2, ESC, 'H'
begin_bold	db	5, ESC, 'p', 0, ESC, 'G'
end_italic	db	2, ESC, '5'
begin_italic	db	2, ESC, '4'
end_under	db	3, ESC, '-', 0
begin_under	db	3, ESC, '-', 1
end_super	db	2, ESC, 'T'
begin_super	db	6, ESC, 'p', 0, ESC, 'S', 0
end_sub		db	2, ESC, 'T'
begin_sub	db	6, ESC, 'p', 0, ESC, 'S', 1
end_lq		db	2, ESC, 'F'
begin_lq	db	5, ESC, 'P', 18, ESC, 'E'
end_expand	db	3, ESC, 'W', 0
begin_expand	db	3, ESC, 'W', 1
end_light	db	0
begin_light	db	0

set_pica	db	6, ESC, 'P', 18, ESC, 'p', 0 ;elite compres propor off
set_elite	db	2, ESC, 'M'		;elite = elite on
set_cmpres	db	3, ESC, 'P', 15		;compres=elite off, compres on
set_propor	db	5, ESC, 'P', ESC, 'p', 1 ;propor = elite off,propor on

; Printer spacing command.
sized_lf	db	3, ESC, 'J', '?'

		end
